plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id 'nu.studer.jooq' version '8.2'
}

group = 'com.deerear'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web' //WEB
	implementation 'org.springframework.boot:spring-boot-starter-websocket' //WEBSOCKET
	implementation 'org.springframework.boot:spring-boot-starter-aop' //AOP

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'io.github.cdimascio:dotenv-java:2.2.0'
	// Swagger UI 의존성
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0' // 최신 버전 사용
	// Spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.jetbrains:annotations:24.0.0'
	// JPA 의존성
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation 'javax.persistence:javax.persistence-api:2.2'
	testImplementation("org.springframework.boot:spring-boot-starter-test") // 테스트를 위한 의존성
	runtimeOnly("com.h2database:h2") // 테스트를 위한 의존성

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Restdoc
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.1'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Jooq
	implementation 'org.jooq:jooq:3.16.9'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	jooqGenerator 'org.postgresql:postgresql'
	runtimeOnly 'org.postgresql:postgresql'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
	useJUnitPlatform()
	outputs.dir snippetsDir

	jvmArgs "-Xshare:off"
}

asciidoctor {
	configurations 'asciidoctorExt'
	baseDirFollowsSourceFile()
	inputs.dir snippetsDir
	dependsOn test
	forkOptions {
		jvmArgs("--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED")
	}
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}

tasks.register('copyDocument', Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << '-parameters'
}

def localProperties = new Properties()
def localFile = rootProject.file('.env')
if (localFile.exists()) {
	localFile.withInputStream { localProperties.load(it) }
}

jooq {
	version = '3.16.9'
	configurations {
		main {
			generateSchemaSourceOnCompilation = true
			generationTool {
				jdbc {
					driver = 'org.postgresql.Driver'
					url = localProperties.getProperty('SPRING_DATASOURCE_URL')
					user = localProperties.getProperty('SPRING_DATASOURCE_USERNAME')
					password = localProperties.getProperty('SPRING_DATASOURCE_PASSWORD')
				}
				generator {
					name = 'org.jooq.codegen.DefaultGenerator'
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						inputSchema = 'public'
					}
					target {
						packageName = 'com.deerear.jooq.generated'
						directory = 'build/generated/jooq'
					}
				}
			}
		}
	}
}

build {
	dependsOn copyDocument
}